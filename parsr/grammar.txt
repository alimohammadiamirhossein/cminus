Program -> Declaration-list
Declaration-list -> Declaration Declaration-list | ε
Declaration -> #declare Declaration-initial Declaration-prime #end
Declaration-initial -> Type-specifier #declare_id #pid ID
Declaration-prime -> Fun-declaration-prime | Var-declaration-prime
Var-declaration-prime -> ; | [ #pnum NUM ] #declare_array #end ;
Fun-declaration-prime -> #declare_func #set_exec #add_scope_Type_state ( #arg_init Params #arg_finish ) #add_scope_Type_function  Compound-stmt #del_scope_Type_function #del_scope_Type_state #jump_return_address
Type-specifier -> int | void
Params -> #declare int #declare_id #pid ID Param-prime #end Param-list | void Param-list-void-abtar
Param-list-void-abtar -> #declare_id #pid ID Param-prime Param-list | ε
Param-list -> , Param Param-list | ε
Param -> #declare Declaration-initial Param-prime #end
Param-prime -> [ ] | ε
Compound-stmt -> { Declaration-list Statement-list }
Statement-list -> Statement Statement-list | ε
Statement -> Expression-stmt | Compound-stmt | Selection-stmt | Iteration-stmt | Return-stmt | For-stmt
Expression-stmt -> Expression #end ; | break #add_break_point_Type_for ; | ;
Selection-stmt -> if ( Expression ) #save #add_scope_Type_if Statement #del_scope_Type_if #add_scope_Type_state #add_break_point_Type_state #jpf_save else #add_scope_Type_if Statement #del_scope_Type_if #fill_break_point_Type_state #end
Iteration-stmt -> while #label ( Expression ) #save #add_scope_Type_for Statement #whilejump #del_scope_Type_for
Return-stmt -> return Return-stmt-prime #add_break_point_Type_function
Return-stmt-prime -> ; | #return_value_push Expression #assign  #end ;
For-stmt -> #add_scope_Type_for for ID = Vars Statement #del_scope_Type_for
Vars -> Var Var-zegond
Var-zegond -> , Var Var-zegond | ε
Var -> ID Var-prime
Expression -> Simple-expression-zegond | #pid ID B
B -> = Expression #assign | [ Expression ] #parray H | Simple-expression-prime
H -> = Expression #assign | G D C
Simple-expression-zegond -> Additive-expression-zegond C
Simple-expression-prime -> Additive-expression-prime C
C -> Relop Additive-expression  #op_exec | ε
Relop -> #op_push < | #op_push ==
Additive-expression -> Term D
Additive-expression-prime -> Term-prime D
Additive-expression-zegond -> Term-zegond D
D -> Addop Term #op_exec D | ε
Addop -> #op_push + | #op_push -
Term -> Signed-factor G
Term-prime -> Signed-factor-prime G
Term-zegond -> Signed-factor-zegond G
G -> #op_push * Signed-factor #op_exec G | ε
Signed-factor -> + Factor | - Factor #negative | Factor
Signed-factor-prime -> Factor-prime
Signed-factor-zegond -> + Factor | - Factor #negative | Factor-zegond
Factor -> ( Expression ) | #pid ID Var-call-prime | #pnum NUM
Var-call-prime -> ( #arg_pass Args ) #function_call | Var-prime
Var-prime -> [ Expression ] #parray | ε
Factor-prime -> ( #arg_pass Args ) #function_call | ε
Factor-zegond -> ( Expression ) | #pnum NUM
Args -> Arg-list | ε
Arg-list -> Expression Arg-list-prime
Arg-list-prime -> , Expression Arg-list-prime | ε